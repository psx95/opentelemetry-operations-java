/*
 * Copyright 2024 Google LLC
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
	id 'java'
	id 'java-library'
	id 'com.github.johnrengelman.shadow'
}

description = 'OpenTelemetry Java Agent Extension that enables authentication support for OTLP exporters'

tasks {
	assemble.dependsOn(shadowJar)
}

shadowJar {
	archiveFileName.set("gcp-auth-extension.jar")
}

jar {
	// Disable standard jar
	enabled = false
}

// For auto-instrumentation agent, used for running integration test
configurations {
	agent
	includedDependencies
}

dependencies {
	annotationProcessor(libraries.auto_service)
	// We use `compileOnly` dependency because during runtime all necessary classes are provided by javaagent itself.
	compileOnly(libraries.auto_service_annotations)
	compileOnly(libraries.opentelemetry_api)
	compileOnly(libraries.opentelemetry_otlp_exporter)
	compileOnly(libraries.opentelemetry_sdk_autoconf)
	compileOnly(libraries.opentelemetry_autoconfigure_spi)

	//  Only dependencies added to `implementation` configuration will be picked up by Shadow plugin
	//  and added to the resulting jar for our extension's distribution.
	implementation(libraries.google_auth)

	// test dependencies
	// OTel instrumentation used in the sample app
	testImplementation platform(libraries.opentelemetry_instrumetation_bom)
	testImplementation('io.opentelemetry.instrumentation:opentelemetry-instrumentation-annotations')
	// logging libs - need to be included in the test app
	testImplementation libraries.slf4j
	testRuntimeOnly testLibraries.slf4j_simple
	includedDependencies libraries.slf4j
	includedDependencies testLibraries.slf4j_simple
	// testing library and tools
	testImplementation("org.testcontainers:testcontainers:1.20.4")
	testImplementation(testLibraries.junit5)
	testRuntimeOnly(testLibraries.junit5_runtime)

	agent agentLibraries.agent
}

// task to copy and rename the Java Auto-Instrumentation Agent into 'libs' folder
tasks.register('CopyAgent', Copy) {
	into layout.buildDirectory.dir("libs")
	from configurations.agent {
		rename "opentelemetry-javaagent(.*).jar", "otel-agent.jar"
	}
}

tasks.register('BuildTestApp', org.gradle.jvm.tasks.Jar) {
	dependsOn 'CopyAgent'
	dependsOn 'shadowJar'
	dependsOn 'BuildDummyBackend'
	archiveFileName.set("auto-instrumented-test-server.jar")
	from(sourceSets.test.output)
	// include dependencies in the JAR which are required during runtime
	from {
		configurations.includedDependencies.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes.put('Main-Class', 'com.google.cloud.opentelemetry.extension.auth.testapp.InstrumentedServer')
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

tasks.register('BuildDummyBackend', org.gradle.jvm.tasks.Jar) {
	archiveFileName.set("dummy-otlp-backend.jar")
	from(sourceSets.test.output)
	// include dependencies in the JAR which are required during runtime
	from {
		configurations.includedDependencies.collect {
			it.isDirectory() ? it : zipTree(it)
		}
	}
	manifest {
		attributes.put('Main-Class', 'com.google.cloud.opentelemetry.extension.auth.testbackend.DummyOTelHttpEndpoint')
	}
	duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

test {
	dependsOn 'BuildTestApp'
	dependsOn 'BuildDummyBackend'
	useJUnitPlatform()
}
